 // Check bank of reactors sending to bank of reactors.
target C {
    timeout: 2 sec,
    fast: true,
};
import MultiportSource from "../MultiportSource.lf";
reactor Destination(
    bank_index:int(0)
) {
    state s:int(0);
    input in:int;
    reaction(in) {=
        printf("Destination %d received: %d.\n", self->bank_index, in->value);
        if (in->value != self->s) {
            printf("ERROR: Expected %d.\n", self->s);
            exit(1);
        }
        self->s += self->bank_index;
    =}
    reaction(shutdown) {=
        if (self->s == 0 && self->bank_index != 0) {
            fprintf(stderr, "ERROR: Destination %d received no input!\n", self->bank_index);
            exit(1);
        }
        printf("Success.\n");
    =}
}

main reactor(width:int(4)) { 
    a = new[width] MultiportSource();
    b = new[width] Destination();
    a.out -> b.in;
}
